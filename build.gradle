plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id("hytale-mod") version "0.+"
}

repositories {
    mavenCentral()
    maven {
        url = "https://maven.hytale-modding.info/releases"
        name = "HytaleModdingReleases"
    }
}

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    } else {
        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        } else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        } else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

if (!project.hasProperty('hytaleHome')) {
    throw new GradleException('Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.');
} else if (!file(project.findProperty('hytaleHome')).exists()) {
    throw new GradleException("Failed to find Hytale at the expected location. Please make sure you have installed the game. The expected location can be changed using the hytale_home property. Currently looking in ${project.findProperty('hytaleHome')}")
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

def createServerRunArguments(String srcDir) {
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/$patchline/package/game/latest/Assets.zip" + '"'
    def modPaths = []
    if (includes_pack.toBoolean()) {
        modPaths << srcDir
    }
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    if (!modPaths.isEmpty()) {
        programParameters += ' --mods="' + modPaths.join(',') + '"'
    }
    println programParameters
    return programParameters
}

tasks.named("jar") {
    enabled = false
}

subprojects {
    apply plugin: 'java'

    ext {
        hytaleHome = "$hytaleHome"
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(java_version)
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption('Xdoclint:-missing', '-quiet')
    }

    jar {
        destinationDirectory = rootProject.layout.buildDirectory.dir("dist")
    }

    // Updates the manifest.json file with the latest properties defined in the
    // build.properties file. Currently we update the version and if packs are
    // included with the plugin.
    tasks.register('updatePluginManifest') {
        def manifestFile = file(getProjectDir().absolutePath + '/src/main/resources/manifest.json')
        doLast {
            if (!manifestFile.exists()) {
                throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
            }
            def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
            manifestJson.Version = version
            manifestJson.IncludesAssetPack = includes_pack.toBoolean()
            manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
        }
    }

    // Makes sure the plugin manifest is up to date.
    tasks.named('processResources') {
        dependsOn 'updatePluginManifest'
    }

    idea.project.settings.runConfigurations {
        "HytaleServer-${project.name}"(org.jetbrains.gradle.ext.Application) {
            mainClass = 'com.hypixel.hytale.Main'
            moduleName = "${project.idea.module.name}.${project.name}.main" // change run configuration to asd

            programParameters = createServerRunArguments(getProjectDir().absolutePath + File.separator + 'src' + File.separator + 'main')
            workingDirectory = serverRunDir.absolutePath
        }
    }
}
