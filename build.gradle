import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    } else {
        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        } else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        } else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

if (!project.hasProperty('hytaleHome')) {
    throw new GradleException('Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.');
} else if (!file(project.findProperty('hytaleHome')).exists()) {
    throw new GradleException("Failed to find Hytale at the expected location. Please make sure you have installed the game. The expected location can be changed using the hytale_home property. Currently looking in ${project.findProperty('hytaleHome')}")
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

jar {
    enabled = false
}

def createServerRunArguments(String srcDir) {
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/$patchline/package/game/latest/Assets.zip" + '"'
    def modPaths = []
    if (includes_pack.toBoolean()) {
        modPaths << srcDir
    }
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    if (!modPaths.isEmpty()) {
        programParameters += ' --mods="' + modPaths.join(',') + '"'
    }
    println programParameters
    return programParameters
}

dependencies {
    // Hytale server dependency -- not available in public repository yet.
    implementation(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    ext {
        hytaleHome = "$hytaleHome"
    }

    jar {
//        destinationDirectory = rootProject.layout.buildDirectory.dir("dist")
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        from("src/main/resources")

    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(java_version)
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption('Xdoclint:-missing', '-quiet')
    }

    dependencies {
        // Hytale server dependency -- not available in public repository yet.
        compileOnly(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))

        compileOnly("org.projectlombok:lombok:1.18.42")
        annotationProcessor("org.projectlombok:lombok:1.18.42")

        implementation("com.google.guava:guava:33.5.0-jre")
    }

    // Updates the manifest.json file with the latest properties defined in the
    // build.properties file. Currently we update the version and if packs are
    // included with the plugin.
    tasks.register('updatePluginManifest') {
        def manifestFile = file(getProjectDir().absolutePath + '/src/main/resources/manifest.json')
        doLast {
            if (!manifestFile.exists()) {
                logger.info("Could not find manifest.json at ${manifestFile.path}!")
                return
            }
            def manifestJson = new JsonSlurper().parseText(manifestFile.text)
            manifestJson.Version = version
            manifestJson.IncludesAssetPack = includes_pack.toBoolean()
            manifestFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(manifestJson))
        }
    }

    // Makes sure the plugin manifest is up to date.
//    tasks.named('processResources') {
//        dependsOn 'updatePluginManifest'
//    }

    idea.project.settings.runConfigurations {
        "HytaleServer-${project.name}"(org.jetbrains.gradle.ext.Application) {
            mainClass = 'com.hypixel.hytale.Main'
            moduleName = "${project.idea.module.name}.${project.name}.main" // change run configuration to asd

            programParameters = createServerRunArguments(getProjectDir().absolutePath + File.separator + 'src' + File.separator + 'main')
            workingDirectory = serverRunDir.absolutePath
        }
    }
}
